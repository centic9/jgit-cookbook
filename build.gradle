apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url "https://repo.eclipse.org/content/groups/releases/"
  }
}

dependencies {
    compile 'org.eclipse.jgit:org.eclipse.jgit:5.2.1.201812262042-r'
    compile 'org.eclipse.jgit:org.eclipse.jgit.archive:5.2.1.201812262042-r'
    compile 'commons-io:commons-io:2.6'
    compile 'org.slf4j:slf4j-simple:1.7.25'

    // optional dependency of commons-compress which is needed by JGit
    compile 'org.tukaani:xz:1.8'

    testCompile 'junit:junit:4.12'
}

wrapper {
    gradleVersion = '4.10.3'
}

task adjustWrapperPropertiesFile {
    doLast {
        ant.replaceregexp(match: '^#.*', replace: '', flags: 'g', byline: true) {
            fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
        }
        new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with {
            it.text = it.readLines().findAll { it }.sort().join('\n')
        }
        ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
    }
}
wrapper.finalizedBy adjustWrapperPropertiesFile
