apply plugin: 'java'

repositories {
  mavenCentral()
  maven {
    url "https://repo.eclipse.org/content/groups/releases/"
  }
}

dependencies {
	implementation 'org.eclipse.jgit:org.eclipse.jgit:7.0.0.202409031743-r'
	implementation 'org.eclipse.jgit:org.eclipse.jgit.archive:7.0.0.202409031743-r'
	implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:7.0.0.202409031743-r'
	implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:7.0.0.202409031743-r'
	implementation 'commons-io:commons-io:2.18.0'
	implementation 'org.slf4j:slf4j-simple:2.0.16'

    // optional dependency of commons-compress which is needed by JGit
    implementation 'org.tukaani:xz:1.9'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.3'
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '8.5'
}

tasks.register('adjustWrapperPropertiesFile') {
    doLast {
        ant.replaceregexp(match: '^#.*', replace: '', flags: 'g', byline: true) {
            fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
        }
        new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with {
            it.text = it.readLines().findAll { it }.sort().join('\n')
        }
        ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
    }
}
wrapper.finalizedBy adjustWrapperPropertiesFile
